use crate::lexer::{Token, LexicalError};
use crate::ast::ast;
use std::str::FromStr;

grammar;

extern {
    type Location = usize;
    type Error = LexicalError;

    enum Token {
        // 기호
        "{" => Token::LBrace,
        "}" => Token::RBrace,
        "(" => Token::LParen,
        ")" => Token::RParen,
        "[" => Token::LBracket,
        "]" => Token::RBracket,
        "," => Token::Comma,
        "..=" => Token::RangeInclusive,
        ".." => Token::Range,
        "=" => Token::Eq,
        ";" => Token::Semicolon,
        "*" => Token::Star,
        "/" => Token::Slash,
        "+" => Token::Plus,
        "-" => Token::Minus,
        ":" => Token::Colon,
        "->" => Token::Arrow,
        // 할당 연산자
        "+=" => Token::AddAssign,
        "-=" => Token::SubAssign,
        "*=" => Token::MulAssign,
        "/=" => Token::DivAssign,
        "%=" => Token::ModAssign,
        "&=" => Token::BitAndAssign,
        "|=" => Token::BitOrAssign,
        "^=" => Token::BitXorAssign,
        "<<=" => Token::ShlAssign,
        ">>=" => Token::ShrAssign,
        // 논리 연산자
        "!" => Token::Bang,
        "&&" => Token::And,
        "||" => Token::Or,
        // 비트 연산자
        "&" => Token::BitAnd,
        "|" => Token::BitOr,
        "^" => Token::BitXor,
        "<<-" => Token::Shl,
        "->>" => Token::Shr,
        // 비교연산자
        "==" => Token::EqEq,
        "!=" => Token::NotEq,
        "<=" => Token::LtEq,
        ">=" => Token::GtEq,
        "<" => Token::Lt,
        ">" => Token::Gt,
        // 키워드
        "let" => Token::Let,
        "const" => Token::Const,
        "static" => Token::Static,
        "fn" => Token::Fn,
        "if" => Token::If,
        "else" => Token::Else,
        "elif" => Token::ElseIf,
        "match" => Token::Match,
        "while" => Token::While,
        "for" => Token::For,
        "in" => Token::In,
        "pub" => Token::Pub,
        "return" => Token::Return,
        "true" => Token::True,
        "false" => Token::False,

        "identifier" => Token::Identifier(<String>),
        "int" => Token::IntegerLiteral(<i64>),
        "float" => Token::FloatLiteral(<f64>),
        "string" => Token::StringLiteral(<String>),
    }
}

pub KitsuneScript: Vec<ast::Statement> = {
    <stmts:Statement*> => stmts
}

pub Statement: ast::Statement = {
    // 변수 선언
    "let" <name:"identifier"> <type_ann:TypeAnnotation?> "=" <value:Expression> ";" => {
        ast::Statement::Let {
            name,
            type_annotation: type_ann,
            value
        }
    },
    "const" <name:"identifier"> <type_ann:TypeAnnotation?> "=" <value:Expression> ";" => {
        ast::Statement::Const {
            visibility: ast::Visibility::Private,
            type_annotation: type_ann,
            name,
            value
        }
    },
    "pub" "const" <name:"identifier"> <type_ann:TypeAnnotation?> "=" <value:Expression> ";" => {
        ast::Statement::Const {
            visibility: ast::Visibility::Public,
            type_annotation: type_ann,
            name,
            value
        }
    },
    "static" <name:"identifier"> <type_ann:TypeAnnotation?> "=" <value:Expression> ";" => {
        ast::Statement::Static {
            visibility: ast::Visibility::Private,
            type_annotation: type_ann,
            name,
            value
        }
    },
    "pub" "static" <name:"identifier"> <type_ann:TypeAnnotation?> "=" <value:Expression> ";" => {
        ast::Statement::Static {
            visibility: ast::Visibility::Public,
            type_annotation: type_ann,
            name,
            value
        }
    },

    // private (기본) 함수 선언
    "fn" <name:"identifier"> "(" <params:ParamList?> ")" <ret_type:ReturnType?> <body:Block> => {
        ast::Statement::Function {
            visibility: ast::Visibility::Private,
            name,
            params: params.unwrap_or_default(),
            return_type: ret_type,
            body,
        }
    },
    // public 함수 선언
    "pub" "fn" <name:"identifier"> "(" <params:ParamList?> ")" <ret_type:ReturnType?> <body:Block> => {
        ast::Statement::Function {
            visibility: ast::Visibility::Public,
            name,
            params: params.unwrap_or_default(),
            return_type: ret_type,
            body,
        }
    },
    // for 문
    "for" <var_name:"identifier"> "in" <iterable:Expression> <body:Block> => {
        ast::Statement::For {
            var_name,
            iterable,
            body,
        }
    },
    // while 문
    "while" <condition:Expression> <body:Block> => {
        ast::Statement::While {
            condition,
            body,
        }
    },
    // return 문
    "return" <expr:Expression?> ";" => {
        ast::Statement::Return(expr)
    },
    // 할당문
    <target:"identifier"> "=" <value:Expression> ";" => {
        ast::Statement::Assignment {
            target,
            value,
        }
    },
    // 복합 할당문
    <target:"identifier"> <op:CompoundAssignOperator> <value:Expression> ";" => {
        ast::Statement::CompoundAssignment {
            target,
            operator: op,
            value,
        }
    },
    // 표현식 문
    <expr:Expression> ";" => {
        ast::Statement::Expression(expr)
    },
}
pub CompoundAssignOperator: ast::AssignOperator = {
    "+=" => ast::AssignOperator::Add,
    "-=" => ast::AssignOperator::Sub,
    "*=" => ast::AssignOperator::Mul,
    "/=" => ast::AssignOperator::Div,
    "%=" => ast::AssignOperator::Mod,
    "&=" => ast::AssignOperator::BitAnd,
    "|=" => ast::AssignOperator::BitOr,
    "^=" => ast::AssignOperator::BitXor,
    "<<=" => ast::AssignOperator::Shl,
    ">>=" => ast::AssignOperator::Shr,
};
pub Block: ast::Block = {
    "{" <stmts:Statement*> "}" => {
        ast::Block { statements: stmts }
    }
}

pub ParamList: Vec<ast::Parameter> = {
    <param:Parameter> => vec![param],
    <mut params:ParamList> "," <param:Parameter> => {
        params.push(param);
        params
    },
}

TypeAnnotation: ast::Type = {
    ":" <ty:TypeExpr> => ty
};

TypeExpr: ast::Type = {
    <name:"identifier"> "<" <params:TypeExprArray> ">" => ast::Type::Generic { name, params },
    <name:"identifier"> => ast::Type::Simple(name),
};

TypeExprArray: Vec<ast::Type> = {
    <ty:TypeExpr> => vec![ty],
    <mut types:TypeExprArray> "," <ty:TypeExpr> => {
        types.push(ty);
        types
    },
};

pub Parameter: ast::Parameter = {
    <name:"identifier"> <param_type:(":" <TypeExpr>)?> => {
        ast::Parameter {
            name,
            param_type,
        }
    }
}

pub ReturnType: String = {
    "->" <ty:"identifier"> => ty
}

pub Expression: Box<ast::Expression> = {
    <range:RangeExpr> => range,
};

// 연산자 우선순위 (낮음 → 높음):
// 1. Range (.., ..=)
// 2. LogicalOr (||)
// 3. LogicalAnd (&&)
// 4. BitOr (|)
// 5. BitXor (^)
// 6. BitAnd (&)
// 7. Comparison (==, !=, <, <=, >, >=)
// 8. BitShift (<<, >>)
// 9. Additive (+, -)
// 10. Multiplicative (*, /, %)
// 11. Unary (-, +, !)
// 12. Term (literals, variables, function calls, parentheses)
pub RangeExpr: Box<ast::Expression> = {
    <start:AdditiveExpr> "..=" <end:AdditiveExpr> => Box::new(ast::Expression::Range {
        start,
        end,
        inclusive: true,
    }),
    <start:AdditiveExpr> ".." <end:AdditiveExpr> => Box::new(ast::Expression::Range {
        start,
        end,
        inclusive: false,
    }),
    <logical_or:LogicalOrExpr> => logical_or,
};
// 2. Logical OR (||)
pub LogicalOrExpr: Box<ast::Expression> = {
    <lhs:LogicalOrExpr> "||" <rhs:LogicalAndExpr> => Box::new(ast::Expression::BinaryOperation {
        lhs,
        operator: ast::Operator::Or,
        rhs,
    }),
    <logical_and:LogicalAndExpr> => logical_and,
};

// 3. Logical AND (&&)
pub LogicalAndExpr: Box<ast::Expression> = {
    <lhs:LogicalAndExpr> "&&" <rhs:BitOrExpr> => Box::new(ast::Expression::BinaryOperation {
        lhs,
        operator: ast::Operator::And,
        rhs,
    }),
    <bit_or:BitOrExpr> => bit_or,
};
// 4. Bitwise OR (|)
pub BitOrExpr: Box<ast::Expression> = {
    <lhs:BitOrExpr> "|" <rhs:BitXorExpr> => Box::new(ast::Expression::BinaryOperation {
        lhs,
        operator: ast::Operator::BitOr,
        rhs,
    }),
    <bit_xor:BitXorExpr> => bit_xor,
};

// 5. Bitwise XOR (^)
pub BitXorExpr: Box<ast::Expression> = {
    <lhs:BitXorExpr> "^" <rhs:BitAndExpr> => Box::new(ast::Expression::BinaryOperation {
        lhs,
        operator: ast::Operator::BitXor,
        rhs,
    }),
    <bit_and:BitAndExpr> => bit_and,
};
// 6. Bitwise AND (&)
pub BitAndExpr: Box<ast::Expression> = {
    <lhs:BitAndExpr> "&" <rhs:ComparisonExpr> => Box::new(ast::Expression::BinaryOperation {
        lhs,
        operator: ast::Operator::BitAnd,
        rhs,
    }),
    <comp:ComparisonExpr> => comp,
};
// 그 다음 우선순위 (1)
pub ComparisonExpr: Box<ast::Expression> = {
    <lhs:ComparisonExpr> <op:ComparisonOperator> <rhs:BitShiftExpr> => Box::new(ast::Expression::BinaryOperation {
        lhs,
        operator: op,
        rhs,
    }),
    <bit_shift:BitShiftExpr> => bit_shift,
};
pub ComparisonOperator: ast::Operator = {
    "==" => ast::Operator::Eq,
    "!=" => ast::Operator::NotEq,
    "<" => ast::Operator::Lt,
    "<=" => ast::Operator::LtEq,
    ">" => ast::Operator::Gt,
    ">=" => ast::Operator::GtEq,
};
// 8. Bit Shift (<<, >>)
pub BitShiftExpr: Box<ast::Expression> = {
    <lhs:BitShiftExpr> "<<-" <rhs:AdditiveExpr> => Box::new(ast::Expression::BinaryOperation {
        lhs,
        operator: ast::Operator::Shl,
        rhs,
    }),
    <lhs:BitShiftExpr> "->>" <rhs:AdditiveExpr> => Box::new(ast::Expression::BinaryOperation {
        lhs,
        operator: ast::Operator::Shr,
        rhs,
    }),
    <add:AdditiveExpr> => add,
};
// 9. Additive (+, -)
pub AdditiveExpr: Box<ast::Expression> = {
    <lhs:AdditiveExpr> "+" <rhs:MultiplicativeExpr> => Box::new(ast::Expression::BinaryOperation {
        lhs,
        operator: ast::Operator::Add,
        rhs,
    }),
    <lhs:AdditiveExpr> "-" <rhs:MultiplicativeExpr> => Box::new(ast::Expression::BinaryOperation {
        lhs,
        operator: ast::Operator::Sub,
        rhs,
    }),
    <mul:MultiplicativeExpr> => mul,
};
// 10. Multiplicative (*, /, %)
pub MultiplicativeExpr: Box<ast::Expression> = {
    <lhs:MultiplicativeExpr> "*" <rhs:UnaryExpr> => Box::new(ast::Expression::BinaryOperation {
        lhs,
        operator: ast::Operator::Mul,
        rhs,
    }),
    <lhs:MultiplicativeExpr> "/" <rhs:UnaryExpr> => Box::new(ast::Expression::BinaryOperation {
        lhs,
        operator: ast::Operator::Div,
        rhs,
    }),
    <unary:UnaryExpr> => unary,
};
// 그 다음 우선순위 (3)
pub UnaryExpr: Box<ast::Expression> = {
    "-" <expr:UnaryExpr> => Box::new(ast::Expression::UnaryOperation {
        operator: ast::UnaryOperator::Neg,
        expr,
    }),
    "+" <expr:UnaryExpr> => Box::new(ast::Expression::UnaryOperation {
        operator: ast::UnaryOperator::Pos,
        expr,
    }),
    "!" <expr:UnaryExpr> => Box::new(ast::Expression::UnaryOperation {
        operator: ast::UnaryOperator::Not,
        expr,
    }),
    <term:Term> => term,
};
// 그 다음 우선순위
pub Term: Box<ast::Expression> = {
    <val:"int"> => {
        Box::new(ast::Expression::Integer(val))
    },
    <val:"float"> => {
        Box::new(ast::Expression::Float(val))
    },
    <val:"string"> => {
        Box::new(ast::Expression::String(val))
    },
    "true" => {
        Box::new(ast::Expression::Boolean(true))
    },
    "false" => {
        Box::new(ast::Expression::Boolean(false))
    },
    "[" <elements:ArgList?> "]" => {
            Box::new(ast::Expression::Array(elements.unwrap_or_default()))
    },
    // 함수 호출
    <name:"identifier"> "(" <args:ArgList?> ")" => {
        Box::new(ast::Expression::FunctionCall {
            name,
            args: args.unwrap_or_default(),
        })
    },

    // 변수
    <name:"identifier"> => {
        Box::new(ast::Expression::Variable(name))
    },
    "(" <Expression> ")",
    // 블록 표현식
    <block:Block> => {
        Box::new(ast::Expression::Block(block))
    },
}

pub ArgList: Vec<Box<ast::Expression>> = {
    <expr:Expression> => vec![expr],
    <mut args:ArgList> "," <expr:Expression> => {
        args.push(expr);
        args
    },
}